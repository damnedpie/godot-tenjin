apply plugin: 'com.android.library'

ext.pluginVersionCode = 1
ext.pluginVersionName = "1.16.5"

android {
    compileSdk 34
    //buildToolsVersion "34.0.0"
    namespace "com.onecat.godottenjin"
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        versionCode pluginVersionCode
        versionName pluginVersionName
    }
    ndkVersion '23.2.8568313'
    libraryVariants.configureEach { variant ->
        variant.outputs.all { output ->
            output.outputFileName = "GodotTenjin.$pluginVersionName.${variant.name}.aar"
        }
    }
}

dependencies {
    implementation 'com.google.android.gms:play-services-ads-identifier:18.1.0'
    implementation 'com.android.installreferrer:installreferrer:2.2'
    implementation 'com.facebook.android:facebook-applinks:17.0.2'
    implementation 'com.tenjin:android-sdk:1.16.5'
    compileOnly fileTree(dir: 'libs', includes: ['godot-lib*.aar'])
}

tasks.register('buildIntoRelease', Copy) {
    group = "build"
    dependsOn tasks.build
    from([buildDir.absolutePath, 'outputs', 'aar'].join(File.separator))
    include("GodotTenjin.${pluginVersionName}.release.aar")
    into([rootDir.absolutePath, 'release'].join(File.separator))
}